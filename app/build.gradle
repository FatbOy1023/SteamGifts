apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId 'net.mabako.steamgifts'
        minSdkVersion 15
        targetSdkVersion 23
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        project.ext.set('archivesBaseName', 'SteamGifts')
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        // ** This is the build flavor you want to pick for running the app locally. **
        chocolate {
            null
        }
        // Release for the beta channel
        strawberry {
            applicationIdSuffix '.beta'
        }
        // Release pushed to non-beta testers
        vanilla {
            // Since we're essentially using stable versions after beta versions (which might use earlier, similar version codes), bump this.
            // In comparison:
            //    v1.2-beta.3 -> 1002003 --> 1002003
            //    v1.2        -> 1002000 --> 1002500
            //    v1.2.3      -> 1002003 --> 1002503
            // This would simply not install. Henceforth, we ensure our 'stable' releases always have a higher version number.
            versionCode = rootProject.ext.versionCode + 500
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        warning 'InvalidPackage' // okio references java.nio, which is not part of android.
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':core')
}
