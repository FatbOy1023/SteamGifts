apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "net.mabako.steamgifts"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        project.ext.set('archivesBaseName', 'SteamGifts')
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        // ** This is the build flavor you want to pick for running the app locally. **
        chocolate {
            ext.enableCrashlytics = false
        }
        // Release for the beta channel
        strawberry {
            null
        }
        // Release pushed to non-beta testers
        vanilla {
            // Since we're essentially using stable versions after beta versions (which might use earlier, similar version codes), bump this.
            // In comparison:
            //    v1.2-beta.3 -> 1002003 --> 1002003
            //    v1.2        -> 1002000 --> 1002500
            //    v1.2.3      -> 1002003 --> 1002503
            // This would simply not install. Henceforth, we ensure our 'stable' releases always have a higher version number.
            versionCode = rootProject.ext.versionCode + 500
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':core')
}
